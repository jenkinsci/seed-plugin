/**
 * Script to generate a branch folder and seed.
 *
 * @see net.nemerosa.jenkins.seed.generator.BranchGenerationStep
 */

/**
 * Branch folder
 */

folder(BRANCH_FOLDER_PATH) {}

/**
 * Branch seed
 */

job("${BRANCH_FOLDER_PATH}/${BRANCH_SEED_NAME}") {
    description "Branch seed for ${BRANCH} in ${PROJECT} - generates the pipeline for the ${BRANCH} branch."

    // SCM configuration
    branchSeedScmExtensionPoint()

    // Branch parameters injection
    branchSeedParametersExtensionPoint()

    /**
     * Pipeline generation
     *
     * Reads information from the property file and generates a Gradle build file
     * for the purpose of download the DSL libraries and extracting the bootstrap
     * script file.
     */
    configure { node ->
        node / 'builders' / 'net.nemerosa.jenkins.seed.generator.PipelineGenerationStep' {
            // The whole pipeline configuration is no longer needed, only project parameters
            // and branch parameters
            // Project
            project PROJECT
            projectScmType PROJECT_SCM_TYPE
            projectScmUrl PROJECT_SCM_URL
            projectScmCredentials PROJECT_SCM_CREDENTIALS
            // Pipeline
            disableDslScript PIPELINE_DISABLE_DSL_SCRIPT
            scriptDirectory PIPELINE_SCRIPT_DIRECTORY
            // Branch
            branch BRANCH
            // Jenkins-safe names
            seedProject SEED_PROJECT
            seedBranch SEED_BRANCH
        }
    }

    /**
     * Defines a Gradle step for the build file generated by the previous step:
     * - downloads the dependencies
     * - extract the DSL bootstrap script from the indicated JAR
     */
    wrappers {
        injectPasswords()
    }
    steps {
        shell '''\
#!/bin/bash
if [ "\${SEED_GRADLE}" == "yes" ]
then
    echo "[seed] Running Gradle based library expansion."
    cd seed
    chmod u+x gradlew
    ./gradlew prepare --refresh-dependencies --info
    cd ..
else
    echo "[seed] No Gradle based library expansion."
fi
echo "DEBUG traces"
pwd
ls -l
echo "DEBUG End traces"
if [ ! -f "${SEED_GROOVY_PATH}" ]
then
    echo "[seed] No seed.groovy (expected path: ${SEED_GROOVY_PATH}) has been generated by the pipeline library nor has been provided by the project".
    exit 1
fi
'''
    }

    /**
     * Injection of environment variables for the DSL
     */
    wrappers {
        environmentVariables {
            env('PROJECT', PROJECT)
            env('PROJECT_SCM_TYPE', PROJECT_SCM_TYPE)
            env('PROJECT_SCM_URL', PROJECT_SCM_URL)
            env('PROJECT_SCM_CREDENTIALS', PROJECT_SCM_CREDENTIALS)
            env('BRANCH', BRANCH)
            env('SEED_PROJECT', SEED_PROJECT)
            env('SEED_BRANCH', SEED_BRANCH)
        }
    }

    /**
     * Runs the script DSL
     */
    steps {
        dsl {
            external '${SEED_GROOVY_PATH}'
            removeAction 'DELETE'
            lookupStrategy 'SEED_JOB'
            ignoreExisting false
            additionalClasspath 'seed/lib/*.jar'
        }
    }

    // Branch seed extensions
    branchSeedDslExtensionPoint()
}

/**
 * Firing the branch seed
 */

if (EVENT_STRATEGY_AUTO == "true") {
    queue("${BRANCH_FOLDER_PATH}/${BRANCH_SEED_NAME}")
}
